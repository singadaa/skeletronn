# Определение персонажей игры.
define e = Character('Эйлин', color="#c8ffc8")
define kk = Character('Кар Кызы', color="#6473f5")
define kb = Character('Кыш Бабай', color="#8cb1fa")
define ar = Character('Артур', color="#33782c")
define arm = Character('Артур (мысли)', color="#89c284")
define alb = Character('Албасты', color="#7a4c16")
define bich = Character('Бичура', color="#992532")
define su = Character('Су-Анасы', color="#301fed")
define n = Character(None, kind=nvl)

#переменные#
define marmelad = False #Если игрок захотел мармелада, если машину выбор пойдет под else#
define skrit = False #Если игрок решить сокрыть правду от КарКызы#

#музыка#
define audio.albasty = "music/albasty.mp3"
define audio.badend = "music/badend.mp3"
define audio.final = "music/final.mp3"
define audio.goodend = "music/goodend.mp3"
define audio.normal = "music/normal.mp3"
define audio.normalend = "music/normalend.mp3"
define audio.panika = "music/panika.mp3"
define audio.sad = "music/sad.mp3"
define audio.suanasy = "music/suanasy.mp3"
define audio.zlo = "music/zlo.mp3"
define audio.sme = "music/03868.mp3"

#кар кызы и кыш бабай думают над тем, как вернуть подарок. Выбора нет, лейбл переткает в лейбл viborigr#

label start: #реплики прописаны, нет заднего фона и спрайтов#
    play music sad fadein 1
    scene stariki
    "Не за горами грохот салютов, звон бокалов и добрых пожеланий на год грядущий в кругу близких.{w}
    И каждый ждёт и верит, что бой курантов означает счастливые перемены."

    "Но всегда ли приготовления идут гладко? Или есть в мире чудеса, не доступные глазу взрослого человека?"


    "*Тяжёлый вздох*"

    kk "Ты бы мог не дышать так тяжко? Сделай лицо попроще."
    kb "Спина болит."
    kk "А подарки кто вернет? Одна я не справлюсь."
    "Нельзя сказать, что Кыш Бабаю так уж не под силу вернуть украденное самому, {w}но всё-таки помощь не помешала бы."
    kb "Знаю я одного мальчика – Артуром звать"
    kk "Хороший хоть?"
    kb "На уроки не опаздывает, бабушек через дорогу переводит, котят кормит и защищает{w} — всё по совести делает."
    kk "Допустим. А искать-то его где?"
    stop music

label viborigr: #выбор между машнкой и мармеладом в письме. Активация одной из переменных marmelad or mashina#
    play music normal fadein 2
    scene povestvovanie with dissolve
    "Тем же утром в одной из казанских квартир можно было слышать, как решались проблемы вселенского масштаба."
    scene arturfraza
    ar "От этого выбора зависит моя жизнь..."
    ar "Много кислых мармеладок или машина на пульте управления?"

    "Артур откинулся на спинку стула, устремив взгляд в потолок.{w}
    Время не ждёт."
    "Если он хочет, чтобы письмо успело дойти до Кыш Бабая,
    то следует принять окончательное решение уже сегодня."
    nvl hide
    nvl clear
    scene viborchik with dissolve
    menu:
        "Что выбрать?"
        "Кислые мармеладки":
            $ marmelad = True
            ar "Самое время начать писать письмо"
        "Машина на пульте управления":
            ar "Самое время начать писать письмо"

label vedma: #ведьма переманивает мальчика#
    scene vedperviyabzac
    "Так тихо могло тянуться и дальше зимнее утро, если бы его размышления не прервал скребущий звук когтей."
    "Артур насторожился. {w}Бабушка никогда не держала в доме даже кошек, что уж говорить о собаках."
    play music panika fadein 1
    scene misliarthura
    ar "А если вор какой вломился?"
    arm "Пасть храбрым героем и защитить коллекцию бабушкиной посуды с цветочками в старом серванте — вот что становится жизненно необходимым."
    arm "Жаль только, что ею так ни разу и не воспользовались…"

    "*Не оттягивая момента, Артур резко повернулся в сторону двери.*"

    scene arturved
    ar "Ты кто?!"

    "В тёмном дверном проёме видна сгорбленная женская фигура."
    "Распущенные жёлтые волосы, длинные когти и третий глаз, следящий за каждым движением.{w} Вот кого, а злого духа в гости точно не звали."

    arm "И как прикажете прогонять это чудо?" #Нервничает#

    alb "Попа-а-а-ался таки" #ухмыляется#
    scene albasryi
    "*Немного поколебавшись, Артур набрался смелости, чтобы спросить ведьму*"
    arm "Зачем Вы здесь!?"
    scene albastyved
    alb "Злость в своем сердце затаила. Суету навести хочу среди людей."
    alb "Уж больно шумное время года. Всё покоя бедной ведьме не дают."
    alb "Даваай, присоединяйся ко мне. Я дам тебе всёё: игрушки, сладкое и никаких забот."

    if marmelad:
        menu:
            "И кислые мармеладки?":
                alb "И кислые мармеладки"
            "Ни за что!":
                jump Karkyz1

    else:
        menu:
            "И машинку на пульте управления?":
                alb "И машинку на пульте управления"
            "Ни за что!":
                scene fonbet
                play sound sme
                "*По всей комнате раздается злобное хихиканье*"
                jump Karkyz1
    scene fonbet
    play sound sme
    "*По всей комнате раздается злобное хихиканье*"


label Karkyz1: #визит каркызы, возможность скрыть правду, активация перменной skrit#
    scene fonbet
    "Албасты исчезла так же внезапно, как и появилась. Но неожиданные визиты на этом не закончились."
    stop music fadeout 2
    play sound "stuk-v-okno-zvuk_001.mp3"
    "Раздался стук в окно. И стоило его лишь приоткрыть, как в комнату ворвался снежный вихрь, а вместе с ним и новое сказочное создание."
    play sound "snow.mp3"
    play music normal fadein 1
    scene viborkar
    ar "А Вы кто такая?" #вопрошал Артур#
    scene govoritkirkizikar
    kk "Здравствуй. Я - Кар Кызы, дочь Кыш Бабая. Много наслышана о тебе, Артур. Рада встрече" #улыбка озарила её лицо#
    scene govoritarturkar
    ar "Доброе утро. И я рад..? Но ради чего вы проделали такой долгий путь?"
    scene govoritkirkizikar
    kk "Помощь твоя нужна. А ты, как я наслышана, настоящий защитник пожилых леди и гроза всех дворовых собак."
    scene govoritarturkar
    ar "Но Вы же не..."

    "*Кар Кызы прервала мальчика, окинув грозным взглядом.*"
    scene govoritkirkizikar
    kk "Ты почему даме такие вещи говоришь?"
    scene viborkar
    "Смягчив свой гнев на милость, девушка наконец рассказывает о случившейся беде."
    "Артур начинает понимать что к чему, в голову закрадывается жажда наживы"

    menu:
        "Как поступить?"
        "Скрыть о своей встрече с Албасты":
            $skrit = True
            $ renpy.notify ("Она запомнит это")
            "Значит Албасты и её проказни — не просто бабушкины сказки? (Вы утаили правду от Кар Кызы)"
        "Рассказать о недавней встрече с Албасты":
            "Значит Албасты и меня обмануть решила? (Вы решили поделиться)"
            $ renpy.notify ("Она запомнит это")

label Toiznachytproda: #основопологающий выбор - самостоятельная ветка или с кем-то#
    scene govoritkirkizikar
    kk "Так и значит."
    scene govoritarturkar
    ar "Новый Год ведь совсем скоро...Как же мы сможем одолеть её и вернуть все украденные подарки?" #волнение#
    scene govoritkirkizikar
    kk "К сожалению, я не знаю, но уверена, что в этом нам может помочь Бичура. {w}Или у тебя тоже есть идея?"
    "*Кар Кызы перевела задумчивый взгляд на своего напарника по спасению праздника.*"
    scene govoritarturkar
    menu:
        "Мне нужна помощь?"
        "Дейстовать самостоятельно":
            ar"Мы с друзьями недавно видели, как на берегу Казанки девушка гребень свой искала."
            ar"Это ведь Су Анасы — хозяйка воды, верно? Думаю, она поможет, если я смогу найти потерянную вещь"
            "Вы решили действовать самостоятельно. Кар Кызы отправится обратно: заботиться о стареньком отце"
            "Настало время набраться смелости, ведь впереди вас ждёт путешествие, полное опасных приключений."
            jump Poiskgrebnya

        "Послушать Кар Кызы и вместе с ней обратиться за помощью к Бичуре":
            ar "Ваша идея звучит надежнее."
            kk "Тогда так и поступим"
            "Настало время набраться смелости, ведь впереди вас ждёт путешествие, полное опасных приключений."
            jump Vdomebichur

    return
label Vdomebichur: #уговоры с бичурой, выбор приводящий к первой концовке#
    play music albasty fadein 1
    scene perviyabzacdom
    "Артур и Кар Кызы стояли на пороге обветшалого помещения. Войти и остаться целыми было той ещё задачей."
    "Сплетница-Бичура была довольно-таки вредным созданием.{w} И пусть она приносила богатство в дом, в котором обитала, жизнь по соседству с ней была невыносимой."
    "Сегодня она добра и с удовольствием принимает угощения в качестве подношения, а уже завтра в тебя летит кирпич."
    scene arturdom
    ar "Это жертва, на которую я готов пойти" #печально подытожил Артур#
    scene karkizidodom
    kk "Пожалуй, сегодня обойдемся без травмоопасного героизма" #не согласилась с идеей мальчика Кар Кызы#
    scene arturdom
    ar "И что же делать?"
    scene bichuradom
    bich "Войти наконец, а не топтаться у порога, - раздался третий голос."
    scene vibordom
    "Напарники по спасению нового года одновременно уставились на создание ростом в полтора аршина.{w} Домовой внимательно изучала нагрянувших гостей."
    scene bichuradom
    bich "Чай? Кофе? Потанцуем? - готовилась начать озорничать она."
    scene karkizidom
    kk "Нам бы помощь не помешала. Ты ближе всех к миру людей.{w} Может быть, слышала о каких-нибудь странностях в последние дни от своих хозяев?"
    scene bichuradom
    bich "Ты чего это меня своими проблемами грузишь, а? Ты чего это мне такое говоришь!?"
    bich "И вообще...{w}нечего портить мне настроение —{w} ну-ка БРЫСЬ!(грозит кулаком)"
    "Бичура разозлилась и кажется не собирается вам помогать"
    scene govoritarturdom
    menu:
        "Что ей сказать?"
        "Возмутиться поведением Бичуры":
            ar "Вам следует перестать быть такой вредной и противной."
            scene bichuradom
            bich "ку**ак башлар, Авызыны с**м сезне!!!"
            "Вы проявили неуважение к сказочному существу."
            "Домовой разозлился и начнал кидать в вас все предметы, что попадутся под руку, выкривая татарские маты."
            "Чувство разочарования в идее Кар Кызы будет преследовать главного героя"
            jump endbadbichura
        "Задобрить Бичуру":
            ar "А не хочешь жить в квартире моей бабушки? Обещаю, что всегда буду отдавать тебе самую большую и вкусную порцию пирогов! Честно-честно"
            scene bichuradom
            bich "Не хочу покидать родной дом, да спасибо за твою доброту. Но, так и быть, поделюсь я, как победить хитрую ведьму. Подойди ближе и слушай внимательно"
            jump endgoodbichura
    return
label Poiskgrebnya: #действует самостоятельно, уговоры с су анасы#
    play music normal fadein 2
    scene perviyabzacgre
    "Набережная всегда была оживлённым местом: особенно в преддверии праздничных дней."
    "Повсюду развешивались гирлянды и прочая новогодняя атрибутика, а радостные люди катались на коньках или прогуливались неспешным шагом."
    "Одним словом — идиллия.{w} И лишь мальчик, торопящийся спасти праздник, нарушал её."
    play music suanasy fadein 2
    scene vtoroyabzacgre
    "Найти хозяйку здешних вод было не столь сложно, как казалось на первый взгляд. Пусть и не сразу, но и пропажу общими усиилями удалось найти."
    "Куда труднее оказалось договориться с Су-анасы."
    scene suanasygre
    su "А губозакаточную машинку ты не хочешь?"
    "Упрямства местному духу не занимать."
    scene arturgre
    ar "А если не отдам гребень? - не теряя надежды."
    scene suanasygre
    su "Куда ж денешься, - злость хозяйки вод начинает усиливаться."
    scene perevviboromgre
    "Солнце постепенно начинало садиться, и времени оставалось не то чтобы много. Будет намного проще, если удастся договориться с ней прямо сейчас."
    menu:
        "Как же всё-таки лучше поступить?"
        "Рискнуть всем и уступить духу":
            scene arturgre
            ar "Ладно, держи свой гербень. Пользуйся на здоровье."
            "Су Анасы благодарна за вашу помощь. Она рассказала вам где искать Албасты"
            jump netHoroshei
        "Потребовать больше информации.":
            scene arturgre
            ar "О нет, я не отдам тебе гребень, пока ты не расскажешь мне больше"
            "Су Анасы удивилась вашей наглости. Силой забрала свой гребень и прогонала прочь. Вам удалось узнать лишь способ отыскать Албасты."
            jump endpredatel
label endbadbichura: #игрок не договорился с бичурой#
    scene fonbi
    play music zlo fadein 2
    "Вам и Кар Кызы не удалось договориться с Бичурой. О способе победить Албасты вы знаете не так много, чтобы удалось прогнать её ещё на долгие годы."
    "Ведьма уже поджидала Артура и Кар Кызы, одаривая их безумным взглядом."
    "Но даже страх перед её силами отступал, когда каждый из них понимал ценность украденных подарков."
    "От них зависела радость людей в новогоднюю ночь."
    "Только вот бороться с ведьмой вашей напарнице пришлось самостоятельно. Но вы были хорошей группой поддержки."
    play music normalend fadein 2
    "Пусть и не в срок, но подарки всё-таки были возвращены, а Новый год — спасен с опозданием."
    "Путешествие заняло много времени. И хоть Артур был рад тому, что сумел спасти праздник, засыпать ему пришлось с чувством грусти:"
    "он не успел вовремя написать письмо Кыш Бабаю."
    scene fondvabi
    "На утро под ёлкой его поджидал утешительный приз."
    scene podarokbi
    "Кар Кызы не смогла рассчитывать на вашу помощь, поэтому в награду вы получили лишь невкусную шоколадку в виде Кыш Бабая."
    if persistent.endbadbichura:
        $ renpy.notify ("Вы уже бывали здесь!")
    else:
        $ renpy.notify ("Открыта новая концовка!")
    "Конец игры!"
    "Конец игры!!!"
    $ persistent.endbadbichura = True

    return

label endgoodbichura: #вы договорились с бичурой#
    play music zlo fadein 2
    scene perviyabzacbig
    "Вместе с Кар Кызы вы сумели договориться с Бичурой,"
    "а та, в свою очередь, на радостях не только поделилась расположением тайного логова ведьмы, но и подсказала, как можно одолеть её."
    "Ведьма уже поджидала Артура и Кар Кызы, одаривая их безумным взглядом."
    "Но даже страх перед её силами отступал, когда каждый из них понимал ценность украденных подарков."
    "От них зависела радость людей в новогоднюю ночь. И мальчик был готов к необходимости незаметно подобраться, пока его напарница отвлекала ведьму."
    scene nitkabig
    "Артур аккуратно воткнул в одежду Албасты иглу, сумев подчинить её до того, как она натворила бы ещё больших бед.{w} Победа была у вас в руках."
    scene fonbig
    play music goodend fadein 2
    "Подарки были возвращены и той же ночью доставлены в срок."
    "Путешествие заняло много времени."
    "И хоть Артур был рад тому, что сумел спасти Новый год, засыпать ему пришлось с чувством грусти: он не успел вовремя написать письмо Кыш Бабаю."
    scene podarokbig
    "Но на утро его ждал сюрприз. Под ёлкой его ждала красная машинка на пульте управления и много кислых мармеладок."
    "Кыш Бабай и Кар Кызы были благодарны вам за помощь."
    if persistent.endgoodbichura:
        $ renpy.notify ("Вы уже бывали здесь!")
    else:
        $ renpy.notify ("Открыта новая концовка!")
    "Конец игры!"
    "Конец игры!!!"
    $ persistent.endgoodbichura = True
    return

label endpredatel: #не договорились с су анасы, нас переманили#
    play music zlo fadein 2
    scene suanasypr
    "Договориться с Су Анасы не получилось. Узнать способ одолеть ведьму не удалось.{w} Так что придется выпутываться из передряги собственными силами."
    scene fonpr
    "Стоило лишь включить предусмотрительно взятый фонарик, как в глаза Артура бросилось бесчисленное число ярких обёрток и коробочек."
    "Уж о чём, а о таком количестве подарков он и мечтать не мог."
    scene albastypr
    "Но и ведьма не заставила себя ждать. Взгляд безумных глаз не отрывался от мальчика ни на секунду. Стоило догадаться, что его прихода ждали."
    alb "Будь как дома"
    "*хищная улыбка появилась не лице Албасты.*"
    play music normalend fadein 2
    "По телу пробежали мурашки. Но дороги назад уже не было,{w} как и прежней жизни."
    scene grinchpr
    "Ведьме удалось переманить наивного мальчика на свою сторону.{w} Но ожидают ли вас впереди приключения?"
    "Сможет ли Артур однажды вернуться к прежней жизни?"
    "В момент распаковки очередного сладкого подарка это стало таким неважным и чем-то далеким.{w} Ведь теперь у него в руках был пожизненный запас кислого мармелада."
    "Подарки в этом году получили лишь вы"
    if persistent.endpredatel:
        $ renpy.notify ("Вы уже бывали здесь!")
    else:
        $ renpy.notify ("Открыта новая концовка!")
    "Конец игры!"
    "Конец игры!!!"
    $ persistent.endpredatel = True
    return

label netHoroshei: #вы договорились с су анасы#
    play music zlo fadein 2
    scene fon
    "Вы сумели договориться с Су Анасы. Способа одолеть ведьму у вас по-прежнему нет, но есть преимущество во времени."
    "Благодаря своей смекалке вам удалось обхитрить Албасты и найти потерянные подарки до её появления."
    "Гора ярких оберток и коробочек самых разных размеров, то и дело привлекали ваше внимание. Осталось сделать последний выбор."
    menu:
        "Что сделать с подарками?"
        "Забрать все подарки себе":
            jump endgrinch
        "Отдать подарки Кар Кызы":
            if skrit:
                jump endnightmares
            else:
                jump endProshenie

    return

label endgrinch: #договорились с су анасы, но после выбора вы забираете подарки себе#
    play music badend fadein 2
    "Идея получить машину на пульте управления и много кислых мармеладок то и дело преследовала вас на протяжении всего путешествия."
    "Артур решил лишь одним глазком подсмотреть, что находилось внутри."
    arm "Ничего страшного не случится. Я потом обратно всё сложу"
    arm "Опа, конфетка. Съем пожалуй одну,{w} ну или две..."
    scene vedma
    "Но за ней последовала третья, четвертая...{w}Счёт времени был потерян, а остановиться казалось невозможным.{w} Проклятье Албасты настигло вас."
    "Теперь вы стали злым духом."
    if persistent.endgrinch:
        $ renpy.notify ("Вы уже бывали здесь!")
    else:
        $ renpy.notify ("Открыта новая концовка!")
    "Конец игры!"
    "Конец игры!!!"
    $ persistent.endgrinch = True
    return

label endnightmares: #договорились  с су анасы и отдали подарки кар кызы, но вас преселдуют ночные кошмары, вас не расколдовывают#
    play music badend fadein 2
    scene good
    "Артур решил, что не стоило терять время.{w} В скором времени украденные подарки были возвращены, а наступающий праздник — спасен."
    scene bad
    "Но всё не могло быть так просто.{w} Вы попались в ловушку Албасты.{w} Вкрадчивый голос ведьмы то и дело преследовал, предрекая встречи в ночных кошмарах."
    "Кар Кызы раскрыла ваше тайное желание украсть подарки"
    "Вы разочаровали Кыш Бабая, поэтому он отказался снимать проклятие ведьмы"
    if persistent.endnightmares:
        $ renpy.notify ("Вы уже бывали здесь!")
    else:
        $ renpy.notify ("Открыта новая концовка!")
    "Конец игры!"
    "Конец игры!!!"
    $ persistent.endnightmares = True
    return

label endProshenie: #договорились с су анасы и отдали подарки кар кызы, и вас простили за ложь#
    play music normalend fadein 2
    "Артур решил, что не стоило терять время.{w} В скором времени украденные подарки были возвращены, а наступающий праздник — спасен."
    "Но всё не могло быть так просто.{w} Вы попались в ловушку Албасты.{w} Вкрадчивый голос ведьмы то и дело преследовал, предрекая встречи в ночных кошмарах."
    scene horosho
    "Вы предупредили Кар Кызы о вашей встерче с Албасты. Вы заслуживаете доверие Кар Кызы."
    "Кыш Бабай своей магией помог резвеять проклятие. Более ни кошмары, ни Албасты не смогли бы добраться до вас. "
    if persistent.endProshenie:
        $ renpy.notify ("Вы уже бывали здесь!")
    else:
        $ renpy.notify ("Открыта новая концовка!")
    "Конец игры!"
    "Конец игры!!!"
    $ persistent.endProshenie = True
    return
